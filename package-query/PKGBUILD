# Contributor: tuxce <tuxce.net@gmail.com>
# Contributor: Skunnyk <skunnyk@archlinux.fr>

pkgname=package-query
pkgver=1.8
pkgrel=1
pkgdesc="Query ALPM and AUR"
arch=('i686' 'x86_64' 'mips64el' 'armv6h' 'armv7h' 'arm')
url="https://github.com/archlinuxfr/$pkgname/"
license=('GPL')
depends=('pacman>=5.0' 'yajl>=2.0')
source=("http://mir.archlinux.fr/releases/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('dd3465672fff2a65db6768a5f20efc9a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --localstatedir=/var --prefix=/usr --sysconfdir=/etc --with-aur-url=https://aur.archlinux.org
  make
}

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
